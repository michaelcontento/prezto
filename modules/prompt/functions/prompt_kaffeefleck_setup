# Pure
# by Sindre Sorhus
# https://github.com/sindresorhus/pure
# MIT License

# For my own and others sanity
# git:
# %b => current branch
# %a => current action (rebase/merge)
# prompt:
# %F => color dict
# %f => reset color
# %~ => current path
# %* => time
# %n => username
# %m => shortname host
# %(?..) => prompt conditional - %(condition.true.false)

# turns seconds into human readable time
# 165392 => 1d 21h 56m 32s
prompt_pure_human_time() {
  local tmp=$1
  local days=$(( tmp / 60 / 60 / 24 ))
  local hours=$(( tmp / 60 / 60 % 24 ))
  local minutes=$(( tmp / 60 % 60 ))
  local seconds=$(( tmp % 60 ))
  (( $days > 0 )) && echo -n "${days}d "
  (( $hours > 0 )) && echo -n "${hours}h "
  (( $minutes > 0 )) && echo -n "${minutes}m "
  echo "${seconds}s"
}

# displays the exec time of the last command if set threshold was exceeded
prompt_pure_cmd_exec_time() {
  local stop=$EPOCHSECONDS
  local start=${cmd_timestamp:-$stop}
  integer elapsed=$stop-$start
  (($elapsed > ${PURE_CMD_MAX_EXEC_TIME:=5})) && prompt_pure_human_time $elapsed
}

prompt_pure_preexec() {
  cmd_timestamp=$EPOCHSECONDS
}

prompt_pure_precmd() {
  vcs_info

  prompt_pure_last_duration="%F{red}`prompt_pure_cmd_exec_time`"

  # reset value since `preexec` isn't always triggered
  unset cmd_timestamp
}

prompt_pure_setup() {
  prompt_opts=(cr subst percent)  

  zmodload zsh/datetime
  autoload -Uz add-zsh-hook
  autoload -Uz vcs_info

  add-zsh-hook precmd prompt_pure_precmd
  add-zsh-hook preexec prompt_pure_preexec

  # prompt turns red if the previous command didn't exit with 0
  local bang='%(?.%F{green}.%F{red})‚ùØ%f'

  # directory consists only of the last part
  local directory='%F{cyan}%c%f'

  # VCS
  zstyle ':vcs_info:*' enable bzr git hg svn
  zstyle ':vcs_info:*' check-for-changes true
  zstyle ':vcs_info:*' stagedstr '%F{yellow}!%f'
  zstyle ':vcs_info:*' unstagedstr '%F{red}!%f'
  zstyle ':vcs_info:*' formats '%F{red}|%F{green}%b%c%u'
  zstyle ':vcs_info:*' actionformats "%F{red}|%F{green}%b%c%u %F{cyan}%a%f"
  zstyle ':vcs_info:git*+set-message:*' hooks git_status
  local vcs='${vcs_info_msg_0_}'

  PROMPT="${directory}${vcs} ${bang} "
  RPROMPT=' $prompt_pure_last_duration'
}

prompt_pure_setup "$@"